Juntao

high

# Malicious issuer can steal quote tokens from other issuers

## Summary

Malicious issuer can steal quote token from other issuers, by exercising **PUT** `option token` with malicious token being `payout token`.

## Vulnerability Detail

1. Malicious issuer crafts a malicious token (`MToken`) which conforms to ERC-20 but **decimals** could be changed:
```solidity
contract MaliciousToken {
    ...

    uint8 public decimals;

    function setDecimals(uint8 _decimals) public {
        decimals = _decimals;
    }

    ...
}
```
2. Malicious issuer [deploys](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L107-L191) an `option token` with `payoutToken_` being `MToken` and `call_` being **false** (**PUT**).
```solidity
    function deploy(
        ERC20 payoutToken_,
        ERC20 quoteToken_,
        uint48 eligible_,
        uint48 expiry_,
        address receiver_,
        bool call_,
        uint256 strikePrice_
    ) external override nonReentrant returns (FixedStrikeOptionToken) {
```
3. Malicious issuer sets `MToken`'s decimals to **18** and calls [create(...)](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L236-L297) method to mint `option token`.
```solidity
    function create(
        FixedStrikeOptionToken optionToken_,
        uint256 amount_
    ) external override nonReentrant {
```
```solidity
        } else {
            // Calculate amount of quote tokens required to mint
            uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());


            // Transfer quote tokens from user
            // Check that amount received is not less than amount expected
            // Handles edge cases like fee-on-transfer tokens (which are not supported)
            uint256 startBalance = quoteToken.balanceOf(address(this));
            quoteToken.safeTransferFrom(msg.sender, address(this), quoteAmount);
            uint256 endBalance = quoteToken.balanceOf(address(this));
            if (endBalance < startBalance + quoteAmount)
                revert Teller_UnsupportedToken(address(quoteToken));
        }
```
Let's assume `amount_` is 100e18 and `strikePrice` is 1e18, so `quoteAmount` is 100e18 (100e18 * 1e18 / 1e18), so mailcious issuer provides **100e18** `quote token` and mints **100e18** `option token`

4. Malicious issuer sets `MToken`'s decimals to **17** and calls [exercise(...)](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L302-L392) method to exercise **100e18** `option token`.
```solidity
    function exercise(
        FixedStrikeOptionToken optionToken_,
        uint256 amount_
    ) external override nonReentrant {
```
```solidity
        uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());
```
```solidity
            } else {
                // Calculate protocol fee (in payout tokens)
                uint256 fee = (amount_ * protocolFee) / FEE_DECIMALS;
                fees[payoutToken] += fee;


                // Transfer proceeds from user
                // Check balances before and after transfer to ensure that the correct amount was transferred
                // @audit this does enable potential malicious option tokens that can't be exercised
                // However, we view it as a "buyer beware" situation that can handled on the front-end
                uint256 startBalance = payoutToken.balanceOf(address(this));
                payoutToken.safeTransferFrom(msg.sender, address(this), amount_);
                uint256 endBalance = payoutToken.balanceOf(address(this));
                if (endBalance < startBalance + amount_)
                    revert Teller_UnsupportedToken(address(payoutToken));


                // Transfer proceeds minus fee to receiver
                payoutToken.safeTransfer(receiver, amount_ - fee);
            }
        }


        // Burn option tokens
        optionToken.burn(msg.sender, amount_);


        if (call) {
            // Transfer payout tokens to user
            payoutToken.safeTransfer(msg.sender, amount_);
        } else {
            // Transfer quote tokens to user
            quoteToken.safeTransfer(msg.sender, quoteAmount);
        }
```
At this time, `quoteAmount` is **1000e18** (100e18 * 1e18 / 1e17), so malicious issuer gets **1000e18** `quote token` in return, the profit is **900e18**.

## Impact

Please note the same vulnerability exists in [reclaim(...)](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L395-L437) method. 
Malicious issuer's gain is other issuers' loss, i.e. other issuers' funds are stolen.

## Code Snippet

https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L236-L297

https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L302-L392

## Tool used

Manual Review

## Recommendation

1. `payout token`'s `decimals` should be persistent to `FixedStrikeOptionToken` and the same value should be used when  exercise and reclaim option token:
```diff
    /// @notice The strike price of the option
    /// @return _strike The option strike price specified in the amount of quote tokens per underlying token
    function strike() public pure returns (uint256 _strike) {
        return _getArgUint256(0x9e);
    }

+   /// @notice The decimals of the payout token
+   /// @return _payoutDecimals The payout token decimals
+   function payoutDecimals() public pure returns (uint8 _payoutDecimals) {
+       return _getArgUint8(0xbe);
+   }

    function getOptionParameters()
        external
        pure
        returns (
            ERC20 payout_,
            ERC20 quote_,
            uint48 eligible_,
            uint48 expiry_,
            address receiver_,
            bool call_,
            uint256 strike_,
            uint8 payoutDecimals_
        )
    {
-       return (payout(), quote(), eligible(), expiry(), receiver(), call(), strike());
+       return (payout(), quote(), eligible(), expiry(), receiver(), call(), strike(), payoutDecimals());
    }
```

OR

2. Add `payoutToken_.decimals()` to OptionTokenHash to ensure the `payout token` could not be used for exercising or reclaiming if its decimals is changed:
```diff
    function _getOptionTokenHash(
        ERC20 payoutToken_,
        ERC20 quoteToken_,
        uint48 eligible_,
        uint48 expiry_,
        address receiver_,
        bool call_,
        uint256 strikePrice_
    ) internal pure returns (bytes32) {
        return
            keccak256(
                abi.encodePacked(
                    payoutToken_,
+                   payoutToken_.decimals(),
                    quoteToken_,
                    eligible_,
                    expiry_,
                    receiver_,
                    call_,
                    strikePrice_
                )
            );
    }
```