berndartmueller

high

# A malicious option token deployer can drain quote token funds from the `FixedStrikeOptionTeller` contract

## Summary

An option token deployer can use a malicious ERC-20 `payoutToken`, which the attacker can use to purposefully manipulate the returned value from the external call to `decimals()`, resulting in stealing quote tokens from the `FixedStrikeOptionTeller` contract.

## Vulnerability Detail

Anyone can deploy a new option with the `deploy` function in the `FixedStrikeOptionTeller` contract. The `deploy` function takes the ERC-20 `payoutToken` and `quoteToken` as arguments, among others.

The provided `payoutToken` (potentially also the `quoteToken`) can be malicious and fully controlled by the deployer of the option token.

This can be exploited by the deployer as there are multiple external calls to the `payoutToken` contract.

Specifically, **put** option tokens can be created with the `FixedStrikeOptionTeller.create` function without properly collateralizing the option tokens with the necessary quote tokens. This is archived by manipulating the `quoteAmount` calculated in line 283 by purposefully providing a very large `payoutToken` decimal amount. As the denominator of the calculation is increasing, the resulting `quoteAmount` is decreasing (potentially even rounded down to 0). Hence, the newly created put option tokens are not properly collateralized.

Following, as the put option tokens have been created for cheap (or free), the option token deployer, also being the `receiver`, can call the `reclaim` function once the option tokens have expired. The reclaimed collateral amount of quote tokens, `quoteAmount`, calculated in line 434, can be inflated by the option token deployer by having a very low decimal value returned from `payoutToken.decimals()`. This results in a high `quoteAmount` value being transferred from the `FixedStrikeOptionTeller` to the attacker, draining quote token funds from other deployed option tokens.

## Impact

Quote tokens can be stolen from the `FixedStrikeOptionTeller` contract, which have been deposited to collateralize other deployed option tokens.

## Code Snippet

[src/fixed-strike/FixedStrikeOptionTeller.sol#L283](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L283)

```solidity
File: FixedStrikeOptionTeller.sol
236: function create(
237:     FixedStrikeOptionToken optionToken_,
238:     uint256 amount_
239: ) external override nonReentrant {
...      // [...]
268:
269:     // Transfer in collateral
270:     // If call option, transfer in payout tokens equivalent to the amount of option tokens being issued
271:     // If put option, transfer in quote tokens equivalent to the amount of option tokens being issued * strike price
272:     if (call) {
...          // [...]
281:     } else {
282:         // Calculate amount of quote tokens required to mint
283: @>      uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals()); // @audit-issue external call to `payoutToken` could be malicious
284:
285:         // Transfer quote tokens from user
286:         // Check that amount received is not less than amount expected
287:         // Handles edge cases like fee-on-transfer tokens (which are not supported)
288:         uint256 startBalance = quoteToken.balanceOf(address(this));
289:         quoteToken.safeTransferFrom(msg.sender, address(this), quoteAmount);
290:         uint256 endBalance = quoteToken.balanceOf(address(this));
291:         if (endBalance < startBalance + quoteAmount)
292:             revert Teller_UnsupportedToken(address(quoteToken));
293:     }
294:
295:     // Mint new option tokens to sender
296:     optionToken.mint(msg.sender, amount_);
297: }
```

[src/fixed-strike/FixedStrikeOptionTeller.sol#L434](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L434)

```solidity
395: function reclaim(FixedStrikeOptionToken optionToken_) external override nonReentrant {
...      // [...]
427:
428:     // Transfer remaining collateral to receiver
429:     uint256 amount = optionToken.totalSupply();
430:     if (call) {
431:         payoutToken.safeTransfer(receiver, amount);
432:     } else {
433:         // Calculate amount of quote tokens equivalent to amount at strike price
434: @>      uint256 quoteAmount = amount.mulDiv(strikePrice, 10 ** payoutToken.decimals()); // @audit-issue external call to `payoutToken` could be malicious
435:         quoteToken.safeTransfer(receiver, quoteAmount);
436:     }
437: }
```

## Tool used

Manual Review

## Recommendation

Consider refraining from making any external calls to potentially untrusted contracts, such as the `payoutToken` (and possibly also the `quoteToken`). Instead, cache and store the number of token decimals in the option token parameters. As the [option token decimals are equal to the token decimals of the `payoutToken`](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L221), consider using the option token's decimals.
