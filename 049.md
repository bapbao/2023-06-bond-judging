berndartmueller

medium

# Depositing staking tokens at a later epoch requires claiming rewards for all epochs since the very first epoch

## Summary

The `_claimRewards` function can possibly revert with an Out-of-gas error due to attempting to claim the rewards for all epochs since epoch 0, as the user's last claimed epoch is not initialized to the epoch at the time of the `stake` call. Or, if not exhausting the gas limit, the user has to pay a lot of gas fees to claim rewards for all epochs since epoch 0.

## Vulnerability Detail

Whenever any of the `stake`, `unstake`, `unstakeAll` and `claimRewards` functions is called, the `_claimRewards` function is called internally to claim rewards for all epochs since the user's last claimed epoch. However, the user's last claimed epoch is not initialized to the epoch when the user first stakes. This means that the `_claimRewards` function will loop all epochs since epoch 0 until the current `epoch`, causing the function to possibly revert due to exhausting the gas limit if too many epochs have passed since the first epoch or, if not exhausting the gas limit, causing the user to pay a lot of gas fees.

## Impact

The `_claimRewards` function can possibly revert due to exhausting the gas limit if too many epochs have passed since the first epoch. This prevents the user from calling the `stake`, `unstake`, `unstakeAll` and `claimRewards` functions, which internally call the `_claimRewards` function.
Instead, the user has to repeatedly call the [`claimNextEpochRewards`](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/liquidity-mining/OTLM.sol#L430) function to claim rewards two epochs at a time. This is a very tedious process for the user and requires a lot of gas.

## Code Snippet

[src/fixed-strike/liquidity-mining/OTLM.sol#L326](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/liquidity-mining/OTLM.sol#L326)

Staking the very first time, i.e., when the user's balance is zero, does not initialize the user's last claimed epoch (`lastEpochClaimed`) to the current epoch. This will leave the `lastEpochClaimed` value initialized to zero.

```solidity
304: function stake(
305:     uint256 amount_,
306:     bytes calldata proof_
307: ) external nonReentrant requireInitialized updateRewards tryNewEpoch {
...      // [...]
318:
319:     // Get user balance, if non-zero, claim rewards before increasing stake
320:     uint256 userBalance = stakeBalance[msg.sender];
321:     if (userBalance > 0) {
322:         // Claim outstanding rewards, this will update the rewards per token claimed
323:         _claimRewards();
324:     } else {
325:         // Initialize the rewards per token claimed for the user to the stored rewards per token
326: @>      rewardsPerTokenClaimed[msg.sender] = rewardsPerTokenStored;
327:     }
328:
...      // [...]
335: }
```

[src/fixed-strike/liquidity-mining/OTLM.sol#L421](https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/liquidity-mining/OTLM.sol#L421)

The `_claimRewards` function will loop all epochs since epoch 0 until the current `epoch`, since the user's last claimed epoch is zero after the initial `stake` call.

```solidity
408: function _claimRewards() internal returns (uint256) {
409:     // Claims all outstanding rewards for the user across epochs
410:     // If there are unclaimed rewards from epochs where the option token has expired, the rewards are lost
411:
412:     // Get the last epoch claimed by the user
413:     uint48 userLastEpoch = lastEpochClaimed[msg.sender];
414:
415:     // If the last epoch claimed is equal to the current epoch, then only try to claim for the current epoch
416:     if (userLastEpoch == epoch) return _claimEpochRewards(epoch);
417:
418:     // If not, then the user has not claimed all rewards
419:     // Start at the last claimed epoch because they may not have completely claimed that epoch
420:     uint256 totalRewardsClaimed;
421: @>  for (uint48 i = userLastEpoch; i <= epoch; i++) { // @audit-info unbounded loop
422: @>      // For each epoch that the user has not claimed rewards for, claim the rewards
423: @>      totalRewardsClaimed += _claimEpochRewards(i);
424: @>  }
425:
426:     return totalRewardsClaimed;
427: }
```

## Tool used

Manual Review

## Recommendation

Consider initializing the user's last claimed epoch (`lastEpochClaimed`) to the current epoch when they first stake with the `stake` function. This would prevent having to claim rewards for all previous epochs.
